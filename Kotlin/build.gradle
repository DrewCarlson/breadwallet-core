plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
    id 'org.jetbrains.dokka' version '0.10.0'
}

def kotlin_version = '1.3.61'
def kotlin_io_version = '0.1.16'

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url 'https://dl.bintray.com/brcore/core' }
}


def publicApiHeaders = file('../include')
    .listFiles()
    .findAll { it.name.endsWith('.h') }
def privateApiHeaders = file('../crypto')
    .listFiles()
    .findAll { it.name.endsWith('.h') }

kotlin {
    jvm()
    macosX64("macos") {
        // TODO(fix): For now a prebuilt static core lib is used for testing
        def coreLibPath = file(".").absolutePath
        binaries.getTest(DEBUG).linkerOpts = ["-L" + coreLibPath]

        compilations.main {
            kotlinOptions {
                freeCompilerArgs += ["-include-binary", coreLibPath + "/libCoreMacOS.a"]
            }
            cinterops {
                BRCrypto {
                    packageName 'brcrypto'
                    headers publicApiHeaders + privateApiHeaders
                    includeDirs '..', '../include', '../crypto', '../support'
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io:$kotlin_io_version"
            }
        }

        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlin_io_version"
                implementation "com.breadwallet.core:corenative-jre:0.0.4"
                // TODO(fix): Guava is missing from the published corenative-jre pom
                implementation "com.google.guava:guava:28.1-jre"
            }
        }

        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            }
        }

        macosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-io-native:$kotlin_io_version"
            }
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"

    multiplatform {
        jvm {}
        macos {}
    }
}
